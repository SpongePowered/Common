// Gradle plugins
buildscript {
    repositories {
        maven {
            name = 'plugins'
            url = 'https://plugins.gradle.org/m2'
        }
        maven {
            name = 'minecrell'
            url = 'http://repo.minecrell.net/releases'
        }
    }

    dependencies {
        // VanillaGradle contains a custom ForgeGradle extension for SpongeCommon (see below)
        classpath 'net.minecrell:VanillaGradle:2.0.2_1'
        classpath('nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0') {
            exclude module: 'guava' // Older version of Guava conflicts with ForgeGradle
        }
    }
}

// Apply our custom ForgeGradle extension for SpongeCommon
// Compared to the regular tweaker-server plugin, SpongeCommon has all run tasks disabled
// and the Minecraft workspace defined as "provided" so they won't conflict with the implementation
apply plugin: 'net.minecrell.vanilla.server.library'
ext.minecraftPlugin = plugins.getPlugin('net.minecrell.vanilla.server.library')
ext.DIR_MCP_MAPPINGS = net.minecraftforge.gradle.common.Constants.DIR_MCP_MAPPINGS

// Apply shared ForgeGradle configuration
ext.common = project
apply from: 'gradle/minecraft.gradle'

// Inherit SpongeCommon version from SpongeAPI
version = api.version

minecraft {
    version = '1.8'
}

dependencies {
    compile api
    compile('org.spongepowered:mixin:0.4.6-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }

    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'

    compile 'com.zaxxer:HikariCP:2.4.1'

    runtime 'org.mariadb.jdbc:mariadb-java-client:1.2.3'
    runtime 'com.h2database:h2:1.4.190'
    runtime 'org.xerial:sqlite-jdbc:3.8.11.2'
}
